apply plugin: 'com.android.library'

dependencies {
    compile rootProject.ext.rxAndroid
    compile rootProject.ext.rxJava
    compile rootProject.ext.rxRelay
    compile rootProject.ext.supportAnnotations

    testCompile rootProject.ext.assertj
    testCompile rootProject.ext.assertjAndroid
    testCompile rootProject.ext.robolectric
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
}

apply plugin: 'jacoco'

jacoco {
    reportsDir = file("${buildDir}/reports")
}

task coverageReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    def coverageSourceDirs = ['src/main/java']

    classDirectories = fileTree(
            dir: "${buildDir}/intermediates/classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       '**/Manifest*.*',
                       'com/android/**/*.class',
                       '**/*Event.class']
    )

    sourceDirectories = files(coverageSourceDirs)
    executionData = files("${buildDir}/jacoco/testDebugUnitTest.exec")

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

apply from: 'gradle-mvn-push.gradle'


